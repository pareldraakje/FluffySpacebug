** Snakes
* 
new: simp 2 14 36475 "fluffyspacebug_pest" 2 2 3800
mesg wrt+ targ 1000 37165 11383 1

**
** snek script
**

**
** Creation script
**

** snek creation
scrp 2 14 36475 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: act1 1 + hit 8 + eat 16 = 25
	bhvr 25
* Set physics attributes
	accg 0
	aero 3
* Set collision attributes
	perm 5
	fric 60
	elas 30
	anim [0 1 255]
	frat 8
* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
	tick 8
endm

**
** Timer script
**

** snek timer
scrp 2 14 36475 9
	doif carr eq null
		setv va00 rand -9 9
		doif va00 lt 0
			setv va00 rand -3 -2
			setv va01 rand -1 1
			mira 1
		elif va00 gt 0
			setv va00 rand 2 3
			setv va01 rand -1 1
			mira 0
		endi
		reps rand 4 12
			setv va02 rtyp grap posx posy
			doif va02 eq 8 or va02 eq 9
				velo va00 va01
			else
				velo rand -2 2 10
			endi
			wait 15
		repe
		doif rand 0 5 eq 0

			setv va02 rtyp grap posx posy
			doif va02 eq 8 or va02 eq 9
			elif va02 eq -1
				kill ownr
			else
				gsub detritus
			endi

			rnge 700
			setv va00 rand 0 3
			esee 2 14 36475
				addv va00 1
			next
			doif va00 lt 3
* Create new snek
				setv va01 posx
				setv va02 post
				subv va02 5
				new: simp 2 14 36475 "fluffyspacebug_pest" 2 2 rand 1200 4000
				mesg wrt+ targ 1000 va01 va02 0
			elif va00 gt 8
				gsub detritus
				targ ownr
				kill ownr
			endi
		endi
	endi
	tick rand 50 85
	subr detritus
		slow
		targ ownr
		setv va00 posl
		setv va01 post
		new: simp 2 10 36463 "betaship_dead" 1 1 rand 1000 3000
		mesg wrt+ targ 1000 va00 va01 0
		targ ownr
		attr 16
		show 0
		wait 20
		kill ownr
		stop
	retn
endm

**
** Activate 1 (push) script
**

** snek play
scrp 2 14 36475 1
	setv va00 velx
	mulv va00 3
	velo va00 -3
	stim writ from 76 0.5
	stim writ from 88 2
	targ from
* geddonase
	chem 69 0.2
* fear toxin
	chem 80 0.2
endm

**
** Hit script
**

** snek hit
scrp 2 14 36475 3
	velo rand -3 3 rand -6 -3
	stim writ from 76 2
	stim writ from 88 2
	targ from
	chem 69 0.5
	chem 80 0.5
endm


**
** Eat script
**

** snek eat
scrp 2 14 36475 12
	stim writ from 76 0.5
	targ from
	chem 69 0.2
	chem 80 0.2
endm

**
** Removal script
**

rscr

** Remove all snekies
enum 2 14 36475
	kill targ
next